# Maintainer: Thomas Dziedzic <gostrc@gmail.com>

pkgname="anvl-util"
pkgver=8.0
pkgrel=1
pkgdesc="Enable USB peripherals"
arch=("armv7h")
url="https://github.com/networkimprov/usb-manager"
license=("none")
depends=('libusbg-git')
makedepends=('git')
install=${pkgname}.install
replaces=('omap-idle'
          'anvl-usb')
conflicts=('omap-idle'
          'anvl-usb')
source=("git+https://github.com/networkimprov/usb-manager.git#commit=c40903de456e59d86b5c7965b20d185b0b562cc9"
        "anvl-usb.service"
        "anvl-getty-usb@.service"
        "anvl-getty.service"
        "70-power-switch-anvl.rules"
        "70-charging-discharging.rules"
        "charging"
        "discharging"
        "omap-idle.service"
        "omap-idle.sh")
md5sums=('SKIP'
         '6d2f20377475b201e737a2e2106462ef'
         'c7dbfdb8a07b0c1d0bab3a2c640b1ac0'
         '22fcb27f5754ffe8f3b03cdafa7314ac'
         '5b47e895f402f687409d8e852019dfe7'
         '004df05f0daf350af95d65a8fc410358'
         'aec20472b37acae45182e403d4b68876'
         'bfd46ab61f6a06e62e3ac562115f488a'
         '69f0c98da6726713bbd1f34924374482'
         '3996e87cafe4d4e8d396e2c0d74a5dc5')

build() {
  cd usb-manager

  make dynamic
}

package() {
  install -D -m 644 "${srcdir}/anvl-getty.service" \
    "${pkgdir}/usr/lib/systemd/system/anvl-getty.service"

  install -D -m 644 "${srcdir}/anvl-usb.service" \
    "${pkgdir}/usr/lib/systemd/system/anvl-usb.service"

  install -D -m 644 "${srcdir}/anvl-getty-usb@.service" \
    "${pkgdir}/usr/lib/systemd/system/anvl-getty-usb@.service"

  install -D -m 755 "${srcdir}/usb-manager/usb-manager" \
    "${pkgdir}/usr/bin/usb-manager"

  # fix enable power switch:
  # https://github.com/networkimprov/arch-packages/issues/11
  # upstream report:
  # https://bugs.freedesktop.org/show_bug.cgi?id=84201
  install -D -m 644 "${srcdir}/70-power-switch-anvl.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-power-switch-anvl.rules"

  # install charging/discharging handler

  install -D -m 644 "${srcdir}/70-charging-discharging.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-charging-discharging.rules"

  install -d \
    "${pkgdir}/usr/lib/anvl-util/charging.d" \
    "${pkgdir}/usr/lib/anvl-util/discharging.d"

  install -m 755 "${srcdir}/charging" \
    "${pkgdir}/usr/lib/anvl-util/charging.d"
  install -m 755 "${srcdir}/discharging" \
    "${pkgdir}/usr/lib/anvl-util/discharging.d"

  # install omap deep sleep support
  install -D -m 644 "${srcdir}/omap-idle.service" \
    "${pkgdir}/usr/lib/systemd/system/omap-idle.service"

  install -D -m 755 "${srcdir}/omap-idle.sh" \
    "${pkgdir}/usr/bin/omap-idle.sh"
}

# vim:set ts=2 sw=2 et:
